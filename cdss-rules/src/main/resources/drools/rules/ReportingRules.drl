import com.aleknik.cdss.cdssservice.model.Diagnosis;
import com.aleknik.cdss.cdssservice.model.dto.Report;
import com.aleknik.cdss.cdssservice.model.Medicine;
import com.aleknik.cdss.cdssservice.model.MedicineType;
import java.util.List;
import java.util.Set;

rule "Chronic patients"
    when
        $diagnosis: Diagnosis(!disease.name.equals("Prehlada") &&
                              !disease.name.equals("Gnoznica"), $patient: patient) over window:time( 730d )
        $report: Report(!chronicPatients.contains($patient))
        Number(intValue > 5) from accumulate(
             Diagnosis(disease.name.equals($diagnosis.disease.name), patient.id == $patient.id) over window:time( 730d ),
             count()
        )
    then
        modify($report){
            getChronicPatients().add($patient);
        }
end


rule "Addict patients"
    when
        Diagnosis($patient: patient) over window:time( 180d )
        $report: Report(!addictPatients.contains($patient))
        accumulate(
             $d: Diagnosis(hasMedicineType(MedicineType.ANALGESIC), patient.id == $patient.id) over window:time( 180d ),
             $count: count(),
             $list: collectList($d)
        )

        eval($count >= 6)

        Set(size >= 3) from accumulate(
             Diagnosis($doc: doctor) from $list,
             collectSet($doc)
        )
    then
        modify($report){
            getAddictPatients().add($patient);
        }
end

rule "Weak immune system patients"
    when
        Diagnosis($patient: patient) over window:time( 365d )
        $report: Report(!weakImmuneSystemPatients.contains($patient))
        accumulate(
             $d: Diagnosis(hasMedicineType(MedicineType.ANTIBIOTIC), patient.id == $patient.id) over window:time( 365d ),
             $count: count(),
             $list: collectList($d)
        )

        eval($count > 10)

        Set(size >= 2) from accumulate(
             Diagnosis($d: disease) from $list,
             collectSet($d)
        )

        not Diagnosis(hasMedicineType(MedicineType.ANALGESIC) || hasMedicineType(MedicineType.OTHER), patient.id == $patient.id) over window:time( 365d )

    then
        modify($report){
            getWeakImmuneSystemPatients().add($patient);
        }
end